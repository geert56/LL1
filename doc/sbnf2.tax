(*
  DOCUMENTATION INFORMATION                               module: LANGUAGE TOOL
  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  system    : HP/9000
  file      : sbnf2.tax
  unit-title: SUPER-BNF CONCRETE SYNTAX
  ref.      : ebnf.lex, sbnf.tax
  author(s) : G.L.J.M. Janssen
  date      : 14-JAN-1988
  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*)

(* SUPER BACKUS-NAUR FORM (SBNF)  EXPRESSED IN ITSELF *)

<Super-BNF-syntax> ::= { <production-rule> } .

<production-rule> ::=
     <nonterminal-symbol>
       <replace-symbol> <expression> <rule-terminator> .

<expression>      ::= { <term> / <choice-symbol> }+ .

<term>            ::= [ { <factor-list> / <line-passages> }+ ] .

<factor-list>     ::= { <factor> }+ .

<factor>          ::= <primary> [ "+" | "*" | "?" ]
                   |  <separated-list>
                   |  <option-construct> .

<primary>         ::= <atomic-symbol>
                   |  "(" <expression> ")"
                   .

<separated-list>  ::=
  "{" <expression> [ <separator-symbol> <atomic-symbol> ] "}" [ "+" | "*" ] .

(* variants and their meaning:

        { E } +         :       ( E ) +
        { E }           :       ( E ) *
        { E } *         =       { E }
        { E / A }       :       [ E { A E } ]
        { E / A } *     =       { E / A }
        { E / A } +     :       E { A E }
        { E / <> }      =       { E }
        [ E ]           =       ( E ) ?
*)

<option-construct> ::= "[" <expression> "]" .

(* meaning:

        [ E ]           :       E | <EMPTY>
*)

<atomic-symbol>   ::= <terminal-symbol>
                   |  <nonterminal-symbol>
                   |  <empty-symbol> .

<line-passages>   ::= { <EOL> }+ .
